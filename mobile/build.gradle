apply plugin: 'com.android.application'

android {
    useLibrary 'org.apache.http.legacy'
    lintOptions {
        checkReleaseBuilds false
    }
    signingConfigs {
        playRelease
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId "com.kuxhausen.huemore"
        versionCode 84 // version code in wear module must kept in sync with this
        versionName "3.2.3"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.playRelease
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    productFlavors {
        playEdition {
            minSdkVersion 10
            targetSdkVersion 22
        }
        compatabilityEdition {
            minSdkVersion 10
            targetSdkVersion 22
        }
    }
    sourceSets {
        release.res.srcDirs = ['res', '../../trademarks']
    }
}

def Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.playRelease.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.playRelease.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.playRelease.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.playRelease.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:gridlayout-v7:18.0.0'
    compile 'com.android.support:design:23.0.1'
    compile 'com.android.support:preference-v7:23.0.1'
    compile 'com.android.support:preference-v14:23.0.1'
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile 'com.google.code.gson:gson:2.2.4'
    compile project(':volley')
    compile project(':lifx')
    playEditionCompile 'com.google.android.gms:play-services-wearable:7.5.0'
    //playEditionWearApp project(':wear')
}
